(define (half-adder a b s c)
  (let ((d (make-wire))
        (e (make-wire)))
    (or-gate a b d)
    (and-gate a b c)
    (inverter c e)
    (and-gate d e s)
    'ok))
(define (full-adder a b c-in sum c-out)
  (let ((s (make-wire))
        (c1 (make-wire))
        (c2 (make-wire)))
    (half-adder b c-in s c1)
    (half-adder a s sum c2)
    (or-gate c1 c2 c-out)
    'ok))
(define (inverter input output)
  (define (inverter-input)
    (let ((new-value (logical-not (get-signal input))))
      (after-delay inverter-delay
                   (lambda ()
                     (set-signal! output new-value)))))
  (add-action! input inverter-input)
  'ok)
(define (logical-not s)
  (cond ((= s 0) 1)
        ((= s 1) 0)
        (else (error "Invalid signal -- LOGICAL-NOT" s))))
(define (and-gate a1 a2 output)
  (define (and-action-procedures)
    (let ((new-value
           (logical-and (get-signal a1) (get-signal a2))))
      (after-delay and-gate-delay
                   (lambda ()
                     (set-signal! output new-value)))))
  (add-action! a1 and-action-procedures)
  (add-action! a2 and-action-procedures)
  'ok)
(define (logical-and a1 a2)
  (cond ((and (= a1 1) (= a2 1)) 1)
        ((and (= a1 0) (= a2 1)) 0)
        ((and (= a1 1) (= a2 0)) 0)
        ((and (= a1 0) (= a2 0)) 0)
        (else (error "Invalid signal -- LOGICAL-AND" a1 a2))))
;ex3.28
(define (or-gate o1 o2 output)
  (define (or-action-procedures)
    (let ((new-value
           (logical-or (get-signal o1) (get-signal o2))))
      (after-delay or-gate-delay
                   (lambda ()
                     (set-signal! output new-value)))))
  (add-action! o1 or-action-procedures)
  (add-action! o2 or-action-procedures)
  'ok)
(define (logical-or a1 a2)
  (cond ((and (= a1 1) (= a2 1)) 1)
        ((and (= a1 0) (= a2 1)) 1)
        ((and (= a1 1) (= a2 0)) 1)
        ((and (= a1 0) (= a2 0)) 0)
        (else (error "Invalid signal -- LOGICAL-OR" a1 a2))))
;ex3.29
(define (or-gate o1 o2 output)
  (let ((io1 (make-wire))
        (io2 (make-wire))
        (io (make-wire)))
    (inverter o1 io1)
    (inverter o2 io2)
    (and-gate io1 io2 io)
    (inverter io output)
    'ok))
;两个输入信号会被同时地取反，所以这两个反门只会占用一个inverter-delay
;or-gate-dely = 2 * inverter-delay + and-gate-delay

;ex3.30
(define (ripple-carry-adder list-A list-B list-S C)
  (if (= (length list-A)
         (length list-B)
         (length list-S))
      (if (= (length list-A) 1)
          (let ((Cn (make-wire))
                (An (car list-A))
                (Bn (car list-B))
                (Sn (car list-S)))
            (full-adder An Bn Cn Sn C))
          (let ((Ak (car list-A))
                (Bk (car list-B))
                (Sk (car list-S))
                (remain-A (cdr list-A))
                (remain-B (cdr list-B))
                (remain-S (cdr list-S))
                (Cin (make-wire)))
            (ripple-carry-adder remain-A remain-B remain-S Cin)
            (full-adder Ak Bk Cin Sk C)))
      (error "Invalid number of signals -- RIPPLE-CARRY-ADDER")))
;n位行波进位器的时延为 n*full-adder-delay
;ripple-carry-adder-delay
; = n * (2 * (and-gate-delay + 3 * inverter-delay) + or-gate-delay)
; = n * (2 * (and-gate-delay + 3 * inverter-delay) + and-gate-delay + 2 * inverter-delay)
; = n * (3 * and-gate-delay + 8 * inverter-delay)
(define (make-wire)
  (let ((signal-value 0) (action-procedures '()))
    (define (set-signal! new-value)
      (if (not (= signal-value new-value))
          (begin (set! signal-value new-value)
                 (call-each action-procedures))
          'done))
  (define (accept-action-procedure! proc)
    (set! action-procedures (cons proc action-procedures))
    (proc))
  (define (dispatch m)
    (cond ((eq? m 'get-signal) signal-value)
          ((eq? m 'set-signal!) set-signal!)
          ((eq? m 'add-action!) accept-action-procedure!)
          (else (error "Unknown operation -- WIRE" m))))
  dispatch))
(define (call-each procedures)
  (if (null? procedures)
      'done
      (begin
        ((car procedures))
        (call-each (cdr procedures)))))
(define (get-signal wire)
  (wire 'get-signal))
(define (set-signal! wire new-value)
  ((wire 'set-signal!) new-value))
(define (add-action! wire action-procedure)
  ((wire 'add-action!) action-procedure))


(define (after-delay delay action)
  (add-to-agenda! (+ delay (current-time the-agenda))
                  action
                  the-agenda))
(define (propagate)
  (if (empty-agenda? the-agenda)
    'done
    (let ((first-item (first-agenda-item the-agenda)))
      (first-item)
      (remove-first-agenda-item! the-agenda)
      (propagate))))
(define (probe name wire)
  (add-action! wire
               (lambda ()
                 (newline)
                 (display name)
                 (display " ")
                 (display (current-time the-agenda))
                 (display " New-value = ")
                 (display (get-signal wire)))))
;ex3.31
; 如果不立即执行的话，agenda中永远不会有任何action。propagate也不会有任何结果。
(load "queue.scm")
(define (make-time-segment time queue)
  (cons time queue))

(define (segment-time s) (car s))
(define (segment-queue s) (cdr s))

(define (make-agenda) (list 0))
(define (current-time agenda) (car agenda))
(define (set-current-time! agenda time)
  (set-car! agenda time))
(define (segments agenda) (cdr agenda))

(define (set-segments! agenda segments)
  (set-cdr! agenda segments))
(define (first-segment agenda) (car (segments agenda)))
(define (rest-segments agenda) (cdr (segments agenda)))

(define (empty-agenda? agenda) (null? (segments agenda)))

(define (add-to-agenda! time action agenda)
  (define (belongs-before? segments)
    (or (null? segments)
        (< time (segment-time (car segments)))))
  (define (make-new-time-segment time action)
    (let ((q (make-queue)))
      (insert-queue! q action)
      (make-time-segment time q)))
  (define (add-to-segments! segments)
    (if (= (segment-time (car segments)) time)
        (insert-queue! (segment-queue (car segments))
                       action)
        (let ((rest (cdr segments)))
          (if (belongs-before? rest)
              (set-cdr!
               segments
               (cons (make-new-time-segment time action)
                     (cdr segments)))
              (add-to-segments! rest)))))
  (let ((segments (segments agenda)))
    (if (belongs-before? segments)
        (set-segments!
         agenda
         (cons (make-new-time-segment time action)
               segments))
        (add-to-segments! segments))))
(define (remove-first-agenda-item! agenda)
  (let ((q (segment-queue (first-segment agenda))))
    (delete-queue! q)
    (if (empty-queue? q)
        (set-segments! agenda (rest-segments agenda)))))
(define (first-agenda-item agenda)
  (if (empty-agenda? agenda)
      (error "Agenda is empty -- FIRST-AGENDA-ITEM")
      (let ((first-seg (first-segment agenda)))
        (set-current-time! agenda (segment-time first-seg))
        (front-queue (segment-queue first-seg)))))

(define the-agenda (make-agenda))
(define inverter-delay 2)
(define and-gate-delay 3)
(define or-gate-delay 5)

;ex3.32
;两种方式的最终结果是一样的，只是中间状态不同。
; (define input-1 (make-wire))
; (define input-2 (make-wire))
; (define sum (make-wire))
; (define carry (make-wire))

; (probe 'sum sum)
; (probe 'carry carry)

; (half-adder input-1 input-2 sum carry)

; (set-signal! input-1 1)
; (set-signal! input-2 1)
; (propagate)

(define (make-bus bit-width)
  (cond ((= bit-width 0) '())
        ((= bit-width 1) (list (make-wire)))
        ((> bit-width 1) (cons (make-wire) (make-bus (- bit-width 1))))
        (else (error "Invalid bit-width -- MAKE-BUS" bit-width))))
(define (get-wire bus k)
  (list-ref bus k))
(define (bus-to-list bus)
  (if (> (length bus) 0)
      (cons (get-signal (car bus))
            (bus-to-list (cdr bus)))
      '()))
(define input-bus-a (make-bus 8))
(define input-bus-b (make-bus 8))
(define output-bus (make-bus 8))
(define carry (make-wire))
(ripple-carry-adder input-bus-a input-bus-b output-bus carry)
(set-signal! (get-wire input-bus-a 3) 1)
(set-signal! (get-wire input-bus-a 2) 1)
(set-signal! (get-wire input-bus-b 3) 1)
(propagate)
(bus-to-list input-bus-a)
(bus-to-list input-bus-b)
(bus-to-list output-bus)
